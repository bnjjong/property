plugins {
    id 'java'
    id 'maven'
    id 'java-library'
    // https://docs.gradle.org/current/userguide/idea_plugin.html
    id 'idea'
    // fat jar
    // https://www.baeldung.com/gradle-fat-jar
    // https://blog.leocat.kr/notes/2017/10/11/gradle-shadowjar-make-fat-jar
    id "com.github.johnrengelman.shadow" version "6.1.0"

    // https://github.com/bintray/gradle-bintray-plugin
    id "com.jfrog.bintray" version "1.8.5"

    id "maven-publish"
}

allprojects {
    sourceCompatibility = 14
    targetCompatibility = 14
}


group 'org.bnjjong'
version '1.1'
def defaultEncoding = 'UTF-8'
compileJava.options.encoding = defaultEncoding




repositories {
    mavenLocal()    // maven local cache repository
    mavenCentral()
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
    maven { url "https://repo1.maven.org/maven2/" }
}

dependencies {
    def lombokVersion = '1.18.16'
    // lombok
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // jetbrains validation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compile group: 'org.jetbrains', name: 'annotations', version: '20.1.0'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-configuration2
    compile group: 'org.apache.commons', name: 'commons-configuration2', version: '2.7'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.7'

    // https://mvnrepository.com/artifact/commons-beanutils/commons-beanutils
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    // https://mvnrepository.com/artifact/net.sf.json-lib/json-lib
    // test json 변환시 사용
    compile group: 'net.sf.json-lib', name: 'json-lib', version: '2.4', classifier:'jdk15'


    // test
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.6.2'
    // https://mvnrepository.com/artifact/org.junit.platform/junit-platform-engine
    testCompile group: 'org.junit.platform', name: 'junit-platform-engine', version: '1.6.2'
    // https://mvnrepository.com/artifact/org.mockito/mockito-junit-jupiter
    // core 는 내장 되어 있다.
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.5.10'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-engine
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    // https://mvnrepository.com/artifact/org.assertj/assertj-core
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.15.0'
}

test {
    // Enable JUnit 5 (Gradle 4.6+).
    useJUnitPlatform {
        //includeTags 'fast', 'smoke & feature-a'
        // excludeTags 'slow', 'ci'
        includeEngines 'junit-jupiter'
        // excludeEngines 'junit-vintage'
    }

    // Always run tests, even when nothing changed.
    dependsOn 'cleanTest'

    // Show test results.
    testLogging {
        //showStandardStreams = true
        events "passed", "skipped", "failed"
    }
}

jar {
    finalizedBy shadowJar
    archiveBaseName.set('property')
    archiveAppendix.set('lib')
    archiveClassifier.set('jdk-14')
}

shadowJar {
    // 필터 처리
    // https://imperceptiblethoughts.com/shadow/configuration/dependencies/#embedding-jar-files-inside-your-shadow-jar
    dependencies {
        exclude(dependency('ch.qos.logback:logback-classic:1.2.3') )
        exclude(dependency('net.sf.json-lib:json-lib:2.4:jdk15') )
    }
    archiveBaseName.set('property')
    archiveAppendix.set('lib')
    archiveClassifier.set('jdk-14-all')
}

// https://reflectoring.io/guide-publishing-to-bintray-with-gradle/#build-sources-and-javadoc-artifacts
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


// https://reflectoring.io/guide-publishing-to-bintray-with-gradle/#define-what-to-publish
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "dogfootmaster"
            name "jordan han"
            email "dogfootmaster@gmail.com"
        }
    }

    scm {
        url "https://github.com/bnjjong/property"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'org.bnjjong'
            artifactId 'property'
            version '1.0.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'An AWESOME lib. Really!')
                root.appendNode('name', 'My Awesome Lib')
                root.appendNode('url', 'https://github.com/bnjjong/property')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'propertyHandler'
        name = 'property-handler'
        userOrg = 'dogfootmaster'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/bnjjong/property.git'
        version {
            name = '1.0.0'
            desc = '1.0.0'
            released  = new Date()
        }
    }

}

